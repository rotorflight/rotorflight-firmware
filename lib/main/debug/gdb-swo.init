#
# SWO Trace configuration for Rotorflight
#
# Use in launch.json:
#
# "preLaunchCommands": [
#     "source ${workspaceFolder}/.../gdb-swo.init"
# ]
#

#
# Following variables are auto-defined by Cortex-Debug
#
# $swoFreq  	-- Baud rate (freq in Hz) from launch.json (swoConfig.swoFrequency)
# $cpuFreq  	-- CPU Freq. in Hz from launch.json (swoConfig.cpuFrequency)
# $swoPortMask 	-- The ITM ports to enable, calculated based on ports used in swoConfig (launch.json)
# $swoFormat 	-- NRZ/UART(2) vs Manchester(1)
#

set $swoFormat = 0x00000002

define SWO_Init

    set language c

# Enable trace - done in the FW
#	set *($DCB_BASE + 0x0000C) = (1 << 24)

# TPI->SPPR Pin protocol register to NRZ/Manchester
	set *($TPI_BASE + 0x000F0) = $swoFormat

# TPI->ACPR = Prescalar
	set *($TPI_BASE + 0x00010) = ($cpuFreq / $swoFreq) - 1

# ITM->LAR To be able to access rest of the ITM
	set *($ITM_BASE + 0x00FB0) = 0xC5ACCE55

# ITM->TCR = ITM_TCR_TraceBusID_Msk | ITM_TCR_SWOENA_Msk | ITM_TCR_SYNCENA_Msk | ITM_TCR_ITMENA_Msk
	set *($ITM_BASE + 0x00E80) = 0x1000D

# ITM->TPR = ITM_TPR_PRIVMASK_Msk
	set *($ITM_BASE + 0x00E40) = 0xF

# ITM->TER = bit-mask calculated by Cortex-Debug. You can set it to 0xFFFFFFFF if you like
	set *($ITM_BASE + 0x00E00) = $swoPortMask

# DWT_CTRL
    set *($DWT_BASE) = 0x400003FE

# TPI->FFCR (Formatter and Flush Control Register)
    set *($TPI_BASE + 0x00304) = 0x00000100

    set language auto
end

document SWO_Init
Usage: SWO_Init
Initializes relevant CoreSight component registers to enable SWO.
end
